// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

// Common
import {IERC20, IERC20Metadata} from "../interfaces/IERC20.sol";

// Forkcast-Defi
import {UserAccount} from "../accounts/UserAccount.sol";
import {AccountFactory} from "../factory/AccountFactory.sol";
import {StrategyRouter} from "../router/StrategyRouter.sol";

// Aave
import {IPool} from "../interfaces/aave-v3/IPool.sol";
import {
    IAaveProtocolDataProvider
} from "../interfaces/aave-v3/IAaveProtocolDataProvider.sol";
import {
    IPoolAddressesProvider
} from "../interfaces/aave-v3/IPoolAddressesProvider.sol";
import {IPriceOracleGetter} from "../interfaces/aave-v3/IPriceOracleGetter.sol";

// Uniswap V4
import {IPoolManager} from "v4-core/src/interfaces/IPoolManager.sol";
import {PoolKey} from "v4-core/src/types/PoolKey.sol";
import {PoolId, PoolIdLibrary} from "v4-core/src/types/PoolId.sol";
import {TickMath} from "v4-core/src/libraries/TickMath.sol";
import {
    LiquidityAmounts
} from "v4-periphery/src/libraries/LiquidityAmounts.sol";
import {
    IPositionManager
} from "v4-periphery/src/interfaces/IPositionManager.sol";
import {
    PositionInfo,
    PositionInfoLibrary
} from "v4-periphery/src/libraries/PositionInfoLibrary.sol";
import {StateLibrary} from "v4-core/src/libraries/StateLibrary.sol";
import {Slot0} from "v4-core/src/types/Slot0.sol";

contract StrategyLens {
    address public admin;

    // -------- AAVE ê´€ë ¨ --------
    IPoolAddressesProvider public immutable AAVE_ADDRESSES_PROVIDER;
    IAaveProtocolDataProvider public immutable AAVE_DATA_PROVIDER;
    IPool public immutable AAVE_POOL;
    IPriceOracleGetter public immutable AAVE_ORACLE;

    // -------- Uniswap V4 ê´€ë ¨ --------
    IPoolManager public immutable UNI_POOL_MANAGER;
    IPositionManager public immutable UNI_POSITION_MANAGER;

    // -------- Forkcast ì „ìš© --------
    AccountFactory public immutable ACCOUNT_FACTORY;
    StrategyRouter public immutable STRATEGY_ROUTER;

    //  ----------------- AAVE Structs -----------------

    /// @dev ìœ ì € ì „ì²´ Aave ìƒíƒœ ìš”ì•½
    struct UserAaveOverview {
        address user;
        address vault;
        uint256 totalCollateralBase;
        uint256 totalDebtBase;
        uint256 availableBorrowBase;
        uint256 currentLiquidationThreshold;
        uint256 ltv;
        uint256 healthFactor;
    }

    /// @dev ìœ ì €ê°€ íŠ¹ì • assetì„ ê¸°ì¤€ìœ¼ë¡œ Aaveì—ì„œ ì–´ë–¤ í¬ì§€ì…˜ ë“¤ê³  ìˆëŠ”ì§€
    struct UserReservePosition {
        address asset;
        uint256 aTokenBalance;
        uint256 stableDebt;
        uint256 variableDebt;
    }

    /// @dev ë¦¬ì €ë¸Œ ë©”íƒ€ ì •ë³´(+ìº¡, paused ìƒíƒœ)
    struct ReserveStaticData {
        address asset;
        string symbol;
        uint256 decimals;
        uint256 ltv;
        uint256 liquidationThreshold;
        uint256 liquidationBonus;
        uint256 reserveFactor;
        bool usageAsCollateralEnabled;
        bool borrowingEnabled;
        bool stableBorrowRateEnabled;
        bool isActive;
        bool isFrozen;
        uint256 borrowCap;
        uint256 supplyCap;
        address aToken;
        address stableDebtToken;
        address variableDebtToken;
        bool paused;
    }

    /// @dev ë‚´ë¶€ í—¬í¼ìš©: ë¦¬ì €ë¸Œ ì„¤ì •ê°’ ëª¨ìŒ
    struct ReserveConfigView {
        uint256 decimals;
        uint256 ltv;
        uint256 liquidationThreshold;
        uint256 liquidationBonus;
        uint256 reserveFactor;
        bool usageAsCollateralEnabled;
        bool borrowingEnabled;
        bool stableBorrowRateEnabled;
        bool isActive;
        bool isFrozen;
    }

    /// @dev ë‚´ë¶€ í—¬í¼ìš©: í† í° ì£¼ì†Œë“¤
    struct ReserveTokensView {
        address aToken;
        address stableDebtToken;
        address variableDebtToken;
    }

    /// @dev ë‚´ë¶€ í—¬í¼ìš©: ìº¡ + paused
    struct ReserveCapsView {
        uint256 borrowCap;
        uint256 supplyCap;
        bool paused;
    }

    /// ê¸ˆë¦¬(APY) ê´€ë ¨
    struct ReserveRateData {
        address asset;
        string symbol; // ğŸ‘ˆ ì´ê±° ë°˜ë“œì‹œ ë“¤ì–´ê°€ì•¼ í•¨
        uint256 liquidityRateRay; // ì˜ˆì¹˜ ê¸ˆë¦¬ (RAY)
        uint256 variableBorrowRateRay; // ë³€ë™ ëŒ€ì¶œ ê¸ˆë¦¬ (RAY)
        uint256 stableBorrowRateRay; // ê³ ì • ëŒ€ì¶œ ê¸ˆë¦¬ (RAY)
    }

    /// ê°€ê²© ê´€ë ¨
    struct AssetPriceData {
        address asset;
        // BASE_CURRENCY ê¸°ì¤€ ê°€ê²© (Aave Oracle ë‹¨ìœ„ ê·¸ëŒ€ë¡œ)
        uint256 priceInBaseCurrency;
    }

    //  ----------------- Uniswap Structs -----------------
    /// ìœ ë‹ˆìŠ¤ì™‘ í¬ì§€ì…˜ + í’€ ì •ë³´
    struct UniPositionOverview {
        address token0;
        address token1;
        uint128 liquidity;
        uint256 amount0Now;
        uint256 amount1Now;
        int24 tickLower;
        int24 tickUpper;
        int24 currentTick;
        uint160 sqrtPriceX96;
    }

    /// @dev Routerì— ì €ì¥ëœ ê¸°ë³¸ í¬ì§€ì…˜ ì •ë³´(ë‚´ë¶€ PositionInfoì˜ ì¶•ì•½ ë²„ì „)
    struct RouterPositionCore {
        address owner;
        address vault;
        address supplyAsset;
        address borrowAsset;
        bool isOpen;
    }

    /// @dev í•œ ì „ëµ í¬ì§€ì…˜ì— ëŒ€í•œ "í†µí•© ë·°"
    ///      - Router ë©”íƒ€ ì •ë³´
    ///      - Uniswap v4 í¬ì§€ì…˜ ìš”ì•½
    ///      - Aave ê³„ì • ìƒíƒœ ìš”ì•½
    struct StrategyPositionView {
        RouterPositionCore core;
        // Uniswap v4 ê´€ë ¨
        address uniToken0;
        address uniToken1;
        uint128 liquidity;
        uint256 amount0Now; // ì§€ê¸ˆ ì „ë¶€ ë¹¼ë©´ ë°›ëŠ” token0
        uint256 amount1Now; // ì§€ê¸ˆ ì „ë¶€ ë¹¼ë©´ ë°›ëŠ” token1
        int24 tickLower;
        int24 tickUpper;
        int24 currentTick;
        uint160 sqrtPriceX96;
        // Aave ê³„ì • ìš”ì•½ (vault ê¸°ì¤€)
        uint256 totalCollateralBase;
        uint256 totalDebtBase;
        uint256 availableBorrowBase;
        uint256 currentLiquidationThreshold;
        uint256 ltv;
        uint256 healthFactor;
    }

    constructor(
        address _admin,
        address _aaveAddressesProvider,
        address _aavePool,
        address _aaveDataProvdier,
        address _accountFactory,
        address _aaveOracle,
        address _uniPoolManager,
        address _uniPositionManager,
        address _strategyRouter
    ) {
        admin = _admin;

        // Aave
        AAVE_ADDRESSES_PROVIDER = IPoolAddressesProvider(
            _aaveAddressesProvider
        );
        AAVE_DATA_PROVIDER = IAaveProtocolDataProvider(_aaveDataProvdier);
        AAVE_POOL = IPool(_aavePool);
        AAVE_ORACLE = IPriceOracleGetter(_aaveOracle);

        // Uniswap
        UNI_POOL_MANAGER = IPoolManager(_uniPoolManager);
        UNI_POSITION_MANAGER = IPositionManager(_uniPositionManager);

        // Forkcast
        ACCOUNT_FACTORY = AccountFactory(_accountFactory);
        STRATEGY_ROUTER = StrategyRouter(_strategyRouter);
    }

    // -------- AAVE ê´€ë ¨ í•¨ìˆ˜ --------

    // ----------------- 1) ìœ ì € â†’ ë³¼íŠ¸ ì¡°íšŒ -----------------
    /// @notice ì§€ê°‘ ì£¼ì†Œë¡œ UserAccount(Valut) ì£¼ì†Œ ì¡°íšŒ
    /// @dev    í¬ì§€ì…˜ ì—†ìœ¼ë©´ vault == address(0)
    function getUserVault(address user) public view returns (address vault) {
        vault = ACCOUNT_FACTORY.accountOf(user);
    }

    // ----------------- 2) ìœ ì € Aave ì „ì²´ ìš”ì•½ -----------------

    /// @notice ìœ ì €ì˜ Aave ì „ì²´ í¬ì§€ì…˜ ìš”ì•½ (HF, ë‹´ë³´/ë¶€ì±„, vault )
    /// @dev    í”„ë¡ íŠ¸ì—ì„œ 'ëŒ€ì‹œë³´ë“œ ìƒë‹¨ ì¹´ë“œ'ì— ê·¸ëŒ€ë¡œ ë„£ì„ ë°ì´í„°
    function getUserAaveOverview(
        address user
    ) external view returns (UserAaveOverview memory ov) {
        address vault = ACCOUNT_FACTORY.accountOf(user);

        if (vault == address(0)) {
            ov = UserAaveOverview({
                user: user,
                vault: address(0),
                totalCollateralBase: 0,
                totalDebtBase: 0,
                availableBorrowBase: 0,
                currentLiquidationThreshold: 0,
                ltv: 0,
                healthFactor: 0
            });
            return ov;
        }

        (
            uint256 totalCollateralBase,
            uint256 totalDebtBase,
            uint256 availableBorrowsBase,
            uint256 currentLiquidationThreshold,
            uint256 ltv,
            uint256 healthFactor
        ) = AAVE_POOL.getUserAccountData(vault);

        ov = UserAaveOverview({
            user: user,
            vault: vault,
            totalCollateralBase: totalCollateralBase,
            totalDebtBase: totalDebtBase,
            availableBorrowBase: availableBorrowsBase,
            currentLiquidationThreshold: currentLiquidationThreshold,
            ltv: ltv,
            healthFactor: healthFactor
        });
    }

    // ----------------- 3) ë¦¬ì €ë¸Œ ë©”íƒ€ë°ì´í„° (ì „ì—­) -----------------

    /// @notice Aave ìƒì˜ ëª¨ë“  ë¦¬ì €ë¸Œ(í† í°)ì— ëŒ€í•œ ì„¤ì •/ìƒíƒœ ì •ë³´
    /// @dev    í”„ë¡ íŠ¸ì—ì„œ 'ì§€ì› ìì‚° ë¦¬ìŠ¤íŠ¸ + LTV, Caps, Paused ì—¬ë¶€' ë³´ì—¬ì¤„ ë•Œ ì‚¬ìš©
    function getAllAaveReserves()
        external
        view
        returns (ReserveStaticData[] memory reserves)
    {
        IAaveProtocolDataProvider.TokenData[] memory tokens = AAVE_DATA_PROVIDER
            .getAllReservesTokens();

        reserves = new ReserveStaticData[](tokens.length);

        for (uint256 i = 0; i < tokens.length; i++) {
            reserves[i] = _getReserveStaticData(
                tokens[i].tokenAddress,
                tokens[i].symbol
            );
        }
    }

    /// @dev ë‚´ë¶€ì—ì„œ struct 3ê°œì— ë‚˜ëˆ  ë‹´ì•„ì„œ stack depth ì¤„ì´ê¸°
    function _getReserveStaticData(
        address asset,
        string memory symbol
    ) internal view returns (ReserveStaticData memory r) {
        ReserveConfigView memory cfg = _getReserveConfigData(asset);
        ReserveTokensView memory t = _getReserveTokensData(asset);
        ReserveCapsView memory caps = _getReserveCapsData(asset);

        r = ReserveStaticData({
            asset: asset,
            symbol: symbol,
            decimals: cfg.decimals,
            ltv: cfg.ltv,
            liquidationThreshold: cfg.liquidationThreshold,
            liquidationBonus: cfg.liquidationBonus,
            reserveFactor: cfg.reserveFactor,
            usageAsCollateralEnabled: cfg.usageAsCollateralEnabled,
            borrowingEnabled: cfg.borrowingEnabled,
            stableBorrowRateEnabled: cfg.stableBorrowRateEnabled,
            isActive: cfg.isActive,
            isFrozen: cfg.isFrozen,
            borrowCap: caps.borrowCap,
            supplyCap: caps.supplyCap,
            aToken: t.aToken,
            stableDebtToken: t.stableDebtToken,
            variableDebtToken: t.variableDebtToken,
            paused: caps.paused
        });
    }

    /// @dev Aave DataProvider: getReserveConfigurationData
    function _getReserveConfigData(
        address asset
    ) internal view returns (ReserveConfigView memory cfg) {
        (
            cfg.decimals,
            cfg.ltv,
            cfg.liquidationThreshold,
            cfg.liquidationBonus,
            cfg.reserveFactor,
            cfg.usageAsCollateralEnabled,
            cfg.borrowingEnabled,
            cfg.stableBorrowRateEnabled,
            cfg.isActive,
            cfg.isFrozen
        ) = AAVE_DATA_PROVIDER.getReserveConfigurationData(asset);
    }

    /// @dev Aave DataProvider: getReserveTokensAddresses
    function _getReserveTokensData(
        address asset
    ) internal view returns (ReserveTokensView memory t) {
        (t.aToken, t.stableDebtToken, t.variableDebtToken) = AAVE_DATA_PROVIDER
            .getReserveTokensAddresses(asset);
    }

    /// @dev Aave DataProvider: getReserveCaps + getPaused(try/catch)
    function _getReserveCapsData(
        address asset
    ) internal view returns (ReserveCapsView memory caps) {
        (caps.borrowCap, caps.supplyCap) = AAVE_DATA_PROVIDER.getReserveCaps(
            asset
        );

        // ë°°í¬ì— ë”°ë¼ ì—†ì„ ìˆ˜ ìˆìœ¼ë‹ˆ try/catch
        try AAVE_DATA_PROVIDER.getPaused(asset) returns (bool isPaused) {
            caps.paused = isPaused;
        } catch {
            caps.paused = false;
        }
    }

    // ----------------- 4) ìœ ì € ê°œë³„ ë¦¬ì €ë¸Œ í¬ì§€ì…˜ -----------------

    /// @notice ìœ ì €ê°€ ì£¼ì–´ì§„ asset ë¦¬ìŠ¤íŠ¸ì— ëŒ€í•´ Aaveì—ì„œ ë“¤ê³  ìˆëŠ” ì˜ˆì¹˜/ë¶€ì±„ ì”ê³  ì¡°íšŒ
    /// @dev í”„ë¡ íŠ¸ì—ì„œ â€œë‚´ í¬ì§€ì…˜ - í† í°ë³„ ìƒì„¸ í…Œì´ë¸”â€ ìš©
    ///         - assets : ì˜ˆ) [AAVE, WBTC]
    ///         - return : ê° assetì— ëŒ€í•´ aToken/StableDebt/VariableDebt
    function getUserReservePositions(
        address user,
        address[] memory assets
    ) public view returns (UserReservePosition[] memory positions) {
        address vault = ACCOUNT_FACTORY.accountOf(user);
        positions = new UserReservePosition[](assets.length);

        if (vault == address(0)) {
            for (uint256 i = 0; i < assets.length; i++) {
                positions[i] = UserReservePosition({
                    asset: assets[i],
                    aTokenBalance: 0,
                    stableDebt: 0,
                    variableDebt: 0
                });
            }
            return positions;
        }

        for (uint256 i = 0; i < assets.length; i++) {
            address asset = assets[i];

            (
                address aToken,
                address stableDebtToken,
                address variableDebtToken
            ) = AAVE_DATA_PROVIDER.getReserveTokensAddresses(asset);

            uint256 aBal = aToken != address(0)
                ? IERC20(aToken).balanceOf(vault)
                : 0;
            uint256 sDebt = stableDebtToken != address(0)
                ? IERC20(stableDebtToken).balanceOf(vault)
                : 0;
            uint256 vDebt = variableDebtToken != address(0)
                ? IERC20(variableDebtToken).balanceOf(vault)
                : 0;

            positions[i] = UserReservePosition({
                asset: asset,
                aTokenBalance: aBal,
                stableDebt: sDebt,
                variableDebt: vDebt
            });
        }
    }

    // ----------------- 5) ìœ ì € ë¦¬ì €ë¸Œ í¬ì§€ì…˜ (ì „ì²´ ë¦¬ì €ë¸Œ ìë™) -----------------

    /// @notice Aaveì— ë“±ë¡ëœ ëª¨ë“  ë¦¬ì €ë¸Œì— ëŒ€í•´ ìœ ì € í¬ì§€ì…˜ ì¡°íšŒ
    /// @dev í”„ë¡ íŠ¸ì—ì„œ "ë‚´ Aave í¬ì§€ì…˜ ì „ì²´ ë³´ê¸°" ë²„íŠ¼ ëˆ„ë¥´ë©´ ì´ê±° í•œ ë°©ì— í˜¸ì¶œí•˜ë©´ ë¨
    function getUserReservePositionsAll(
        address user
    ) external view returns (UserReservePosition[] memory positions) {
        IAaveProtocolDataProvider.TokenData[] memory tokens = AAVE_DATA_PROVIDER
            .getAllReservesTokens();

        address[] memory assets = new address[](tokens.length);
        for (uint256 i = 0; i < tokens.length; i++) {
            assets[i] = tokens[i].tokenAddress;
        }

        positions = getUserReservePositions(user, assets);
    }

    // ----------------- 6) ë¦¬ì €ë¸Œ ê¸ˆë¦¬(APY) -----------------

    /// @notice ë‹¨ì¼ ë¦¬ì €ë¸Œì˜ ê¸ˆë¦¬ ì •ë³´ (RAY ë‹¨ìœ„)
    function getReserveRates(
        address asset
    ) external view returns (ReserveRateData memory r) {
        (
            uint256 unbacked,
            uint256 accruedToTreasuryScaled,
            uint256 totalAToken,
            uint256 totalStableDebt,
            uint256 totalVariableDebt,
            uint256 liquidityRate,
            uint256 variableBorrowRate,
            uint256 stableBorrowRate,
            uint256 averageStableBorrowRate,
            uint256 liquidityIndex,
            uint256 variableBorrowIndex,
            uint40 lastUpdateTimestamp
        ) = AAVE_DATA_PROVIDER.getReserveData(asset); // v2 D
        // ë‹¨ì¼ ìì‚°ì´ë‹ˆê¹Œ ì‹¬ë³¼ì€ ê·¸ëƒ¥ ERC20 ë©”íƒ€ë°ì´í„°ì—ì„œ ì½ì–´ì˜¤ë©´ ë¨
        string memory symbol = IERC20Metadata(asset).symbol();

        r = ReserveRateData({
            asset: asset,
            symbol: symbol, // ğŸ‘ˆ ì´ ì¤„ ì¶”ê°€
            liquidityRateRay: liquidityRate,
            variableBorrowRateRay: variableBorrowRate,
            stableBorrowRateRay: stableBorrowRate
        });
    }

    /// @notice ëª¨ë“  ë¦¬ì €ë¸Œì— ëŒ€í•œ ê¸ˆë¦¬ ì •ë³´
    function getAllReserveRates()
        external
        view
        returns (ReserveRateData[] memory rates)
    {
        IAaveProtocolDataProvider.TokenData[] memory tokens = AAVE_DATA_PROVIDER
            .getAllReservesTokens();

        uint256 len = tokens.length;
        rates = new ReserveRateData[](len); // âœ… ë°˜ë“œì‹œ new

        for (uint256 i = 0; i < len; ++i) {
            address asset = tokens[i].tokenAddress;

            (
                uint256 unbacked,
                uint256 accruedToTreasuryScaled,
                uint256 totalAToken,
                uint256 totalStableDebt,
                uint256 totalVariableDebt,
                uint256 liquidityRate,
                uint256 variableBorrowRate,
                uint256 stableBorrowRate,
                uint256 averageStableBorrowRate,
                uint256 liquidityIndex,
                uint256 variableBorrowIndex,
                uint40 lastUpdateTimestamp
            ) = AAVE_DATA_PROVIDER.getReserveData(asset); // v2 DataProviderëŠ” ì—¬ê¸°ê¹Œì§€ 10ê°œ(return 10ê°œ)ì„

            rates[i].asset = asset;
            rates[i].symbol = tokens[i].symbol;
            rates[i].liquidityRateRay = liquidityRate;
            rates[i].variableBorrowRateRay = variableBorrowRate;
            rates[i].stableBorrowRateRay = stableBorrowRate;
        }
    }

    // ----------------- 7) Aave ì˜¤ë¼í´ ê°€ê²© -----------------

    /// @notice ë‹¨ì¼ ìì‚° ê°€ê²© (BASE_CURRENCY ê¸°ì¤€)
    function getAssetPrice(address asset) external view returns (uint256) {
        return AAVE_ORACLE.getAssetPrice(asset);
    }

    /// @notice ì—¬ëŸ¬ ìì‚° ê°€ê²© (asset + price ë¬¶ì–´ì„œ ë¦¬í„´)
    function getAssetsPrices(
        address[] calldata assets
    ) external view returns (AssetPriceData[] memory prices) {
        uint256 len = assets.length;
        uint256[] memory rawPrices = AAVE_ORACLE.getAssetsPrices(assets);
        prices = new AssetPriceData[](len);

        for (uint256 i; i < len; ++i) {
            prices[i] = AssetPriceData({
                asset: assets[i],
                priceInBaseCurrency: rawPrices[i]
            });
        }
    }

    /// @notice ì˜¤ë¼í´ ê¸°ì¤€ í†µí™” & ë‹¨ìœ„ (í”„ë¡ íŠ¸ì—ì„œ ìŠ¤ì¼€ì¼ë§ ê³„ì‚°ìš©)
    function getOracleBaseCurrency()
        external
        view
        returns (address baseCurrency, uint256 baseUnit)
    {
        baseCurrency = AAVE_ORACLE.BASE_CURRENCY();
        baseUnit = AAVE_ORACLE.BASE_CURRENCY_UNIT();
    }

    // =========================================================
    //                  Uniswap V4 ë·° í•¨ìˆ˜
    // =========================================================

    /// @notice íŠ¹ì • ìœ ì € + tokenId ê¸°ì¤€ìœ¼ë¡œ Uniswap V4 í¬ì§€ì…˜ ìƒíƒœ ì¡°íšŒ
    /// @dev    í”„ë¡ íŠ¸ì—ì„œ "ë‚´ í¬ì§€ì…˜ ì¹´ë“œ" í•˜ë‚˜ ë Œë”ë§í•  ë•Œ ë”± ì“°ê¸° ì¢‹ì€ í˜•íƒœ
    function getUserUniPosition(
        address user,
        uint256 tokenId
    ) external view returns (UniPositionOverview memory ov) {
        address vault = ACCOUNT_FACTORY.accountOf(user);
        if (vault == address(0)) {
            return ov;
        }

        // 1) StrategyRouter- previewUniPosition
        (
            address token0,
            address token1,
            uint128 liquidity,
            uint256 amount0Now,
            uint256 amount1Now,
            int24 tickLower,
            int24 tickUpper,
            int24 currentTick,
            uint160 sqrtPriceX96
        ) = STRATEGY_ROUTER.previewUniPosition(tokenId);

        // 2) structì— ë‹´ì•„ì„œ ë°˜í™˜
        ov = UniPositionOverview({
            token0: token0,
            token1: token1,
            liquidity: liquidity,
            amount0Now: amount0Now,
            amount1Now: amount1Now,
            tickLower: tickLower,
            tickUpper: tickUpper,
            currentTick: currentTick,
            sqrtPriceX96: sqrtPriceX96
        });
    }

    // =========================================================
    //                  í†µí•© í¬ì§€ì…˜ ë·° í•¨ìˆ˜
    // =========================================================
    /// @notice tokenId ê¸°ì¤€ìœ¼ë¡œ ì´ ì „ëµ í¬ì§€ì…˜ì˜ ì „ì²´ ë·°ë¥¼ í•œ ë²ˆì— ê°€ì ¸ì˜¨ë‹¤.
    /// @dev í”„ë¡ íŠ¸ì—ì„œ "ì „ëµ ìƒì„¸ ì¹´ë“œ" í•˜ë‚˜ ê·¸ë¦´ ë•Œ ì´ê±° í•œ ë°©ì— ì“°ë©´ ë¨.
    function getStrategyPositionView(
        uint256 tokenId
    ) external view returns (StrategyPositionView memory v) {
        // 1) Routerì— ì €ì¥ëœ í¬ì§€ì…˜ ë©”íƒ€ ì •ë³´ ë¡œë”©
        RouterPositionCore memory core;
        (
            core.owner,
            core.vault,
            core.supplyAsset,
            core.borrowAsset,
            core.isOpen
        ) = STRATEGY_ROUTER.positions(tokenId);

        v.core = core;

        // vaultê°€ ì—†ìœ¼ë©´ (ì•„ì§ í¬ì§€ì…˜ ì—†ëŠ” ìƒíƒœ) ë‚˜ë¨¸ì§€ëŠ” ì „ë¶€ 0ìœ¼ë¡œ ë¦¬í„´
        if (core.vault == address(0)) {
            return v;
        }

        // 2) Uniswap v4 í¬ì§€ì…˜ ìš”ì•½
        (
            v.uniToken0,
            v.uniToken1,
            v.liquidity,
            v.amount0Now,
            v.amount1Now,
            v.tickLower,
            v.tickUpper,
            v.currentTick,
            v.sqrtPriceX96
        ) = STRATEGY_ROUTER.previewUniPosition(tokenId);

        // 3) Aave ê³„ì • ìƒíƒœ (vault ê¸°ì¤€)
        (
            v.totalCollateralBase,
            v.totalDebtBase,
            v.availableBorrowBase,
            v.currentLiquidationThreshold,
            v.ltv,
            v.healthFactor
        ) = AAVE_POOL.getUserAccountData(core.vault);
    }
}
